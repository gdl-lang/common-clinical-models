archetype (adl_version=1.4)
	openEHR-EHR-EVALUATION.bmv_model.v0

concept
	[at0000]	-- Bmv model
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Therese Lindberg">
		["email"] = <"therese.lindberg@cambio.se">
		["organisation"] = <"Cambio CDS">
		["date"] = <"2020-06-30">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"Calculates rate of distant brain failure after stereotactic radiosurgery for brain metastasis. Stratifying patients into high and low BMV categories can allow providers to better counsel patients on their risks of requiring additional treatments in the future, such additional SRS or whole brain radiation therapy. ">
			use = <"Use for patients with any primary cancer who have developed a distant brain failure (new brain metastasis) after upfront stereotactic radiosurgery (SRS).

Developed to predict outcomes at the time of salvage treatment after upfront SRS.
Correlates with overall survival, neurologic death, and time to additional salvage treatments by placing patients into low, intermediate, and high risk groups.
Primary cancers with the largest representation in the study were breast, lung, RCC, and melanoma.

BMV, per year = x / y,
where the variables are as follows:
x = number of new brain metastases since initial SRS
y = total time interval between initial SRS and appearance of new brain metastases, in years.

Interpretation:
BMV                     Risk group                 Median overall survival
<4                        Low                           12.4 months
4-13                     Intermediate               8.2 months
>13                      High                           4.3 months">
			keywords = <"brain", "metastasis", "velocity", "BMV", "model", "SRS", "time">
			misuse = <"Not appropriate for use in patients who have had prior whole brain radiation therapy.">
			copyright = <"Cambio CDS">
		>
	>
	lifecycle_state = <"0">
	other_contributors = <>
	other_details = <
		["references"] = <"1. Farris M, Mctyre ER, Cramer CK, et al. Brain Metastasis Velocity: A Novel Prognostic Metric Predictive of Overall Survival and Freedom From Whole-Brain Radiation Therapy After Distant Brain Failure Following Upfront Radiosurgery Alone. Int J Radiat Oncol Biol Phys. 2017;98(1):131-141.">
		["current_contact"] = <"Therese Lindberg, Cambio CDS<therese.lindberg@cambio.se>">
		["MD5-CAM-1.0.1"] = <"4C1F0499FD996F2551C2A21FC1141D33">
	>

definition
	EVALUATION[at0000] matches {	-- Bmv model
		data matches {
			ITEM_TREE[at0001] matches {	-- Tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0002] occurrences matches {0..1} matches {	-- Number of new brain metastases
						value matches {
							C_DV_QUANTITY <
								property = <[openehr::380]>
								list = <
									["1"] = <
										units = <"1">
										magnitude = <|>=0.0|>
									>
								>
							>
						}
					}
					ELEMENT[at0003] occurrences matches {0..1} matches {	-- Time interval between initial SRS and appearance of new brain metastases
						value matches {
							C_DV_QUANTITY <
								property = <[openehr::128]>
								list = <
									["1"] = <
										units = <"a">
									>
								>
							>
						}
					}
					ELEMENT[at0005] occurrences matches {0..1} matches {	-- BMV
						value matches {
							C_DV_QUANTITY <
								property = <[openehr::380]>
								list = <
									["1"] = <
										units = <"1">
										magnitude = <|>=0.0|>
									>
								>
							>
						}
					}
					ELEMENT[at0006] occurrences matches {0..1} matches {	-- Risk Group
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0007, 	-- Low
									at0008, 	-- Intermediate
									at0009]	-- High
								}
							}
						}
					}
					ELEMENT[at0010] occurrences matches {0..1} matches {	-- Median overall survival
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0011, 	-- 12.4 months
									at0012, 	-- 8.2 months
									at0013]	-- 4.3 months
								}
							}
						}
					}
					ELEMENT[at0014] occurrences matches {0..1} matches {	-- Warning Number of metastases
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0016] occurrences matches {0..1} matches {	-- Warning Time interval
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0015] occurrences matches {0..1} matches {	-- BMV special case
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
		}
	}

ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Bmv model">
					description = <"Brain Metastasis Velocity (BMV) Model calculates rate of distant brain failure after stereotactic radiosurgery for brain metastasis.">
				>
				["at0001"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Number of new brain metastases">
					description = <"Since initial SRS; enter as a whole number.">
				>
				["at0003"] = <
					text = <"Time interval between initial SRS and appearance of new brain metastases">
					description = <"If 18 months, enter 1.5, etc.">
				>
				["at0005"] = <
					text = <"BMV">
					description = <"*">
				>
				["at0006"] = <
					text = <"Risk Group">
					description = <"*">
				>
				["at0007"] = <
					text = <"Low">
					description = <"BMV <4 ">
				>
				["at0008"] = <
					text = <"Intermediate">
					description = <"BMV 4-13">
				>
				["at0009"] = <
					text = <"High">
					description = <"BMV >13">
				>
				["at0010"] = <
					text = <"Median overall survival">
					description = <"*">
				>
				["at0011"] = <
					text = <"12.4 months">
					description = <"Risk group= Low">
				>
				["at0012"] = <
					text = <"8.2 months">
					description = <"Risk group= Intermediate">
				>
				["at0013"] = <
					text = <"4.3 months">
					description = <"Risk group= High">
				>
				["at0014"] = <
					text = <"Warning Number of metastases">
					description = <"*">
				>
				["at0015"] = <
					text = <"BMV special case">
					description = <"BMV per year for special cases: divition by 0 and 0 input. ">
				>
				["at0016"] = <
					text = <"Warning Time interval">
					description = <"*">
				>
			>
		>
	>
