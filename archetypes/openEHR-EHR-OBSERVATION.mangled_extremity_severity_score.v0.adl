archetype (adl_version=1.4)
	openEHR-EHR-OBSERVATION.mangled_extremity_severity_score.v0

concept
	[at0000]	-- Mangled extremity severity score
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Rashmi Damodaran">
		["email"] = <"models@cambiocds.com">
		["organisation"] = <"Cambio CDS">
		["date"] = <"2020-06-08">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To hold the values for each variable required to calculate the MESS Score.">
			use = <"Used to provide an early prognosis on injured limb at the time of admission to Emergency Department and is applied to all adult patients.

Utilizes variables, age, limb ischemia, shock, and mechanism of injury, that are readily obtained without any complicated measurements. 

Formula:

MESS Score is calculated by addition of selected points of all the four variables.  
If the limb ischemia is present for > 6 hours, the limb ischemia points are doubled.

Criteria                                                    Values                                                           Points

Limb ischemia                        Reduced pulse but normal perfusion                          1
                                             Pulseless, Paresthesias, Slow caplillary refill                    2
                                                Cool, Paralysis, Numb/insensate                                   3

Patient Age Range                                  < 30                                                                  0
                                                          30-50                                                                        1
                                                           >50                                                                          2
Shock                                            Systolic BP >90mmHg consistently                        0
                                                       Hypotension Transiently                                          1
                                                       Persistent Hypotension                                            2 
Injury Mechanism                 Low Energy (Stab, gunshot, simple fracture)              1
                                      Medium Energy (Dislocations, open/multiple fractures)     2
                                       High Energy (High speed MVA or rifle shot)                         3
                                       Very High energy (High speed trauma with gross              4
                                       contamination)     
Interpretation:
MESS Score  < 7      -      High likelihood of limb/extremity viability (predictive of salvaged limb but
                                        cases of amputation in patients with MESS Score <7 found in validation studies)
MESS Score >=7    -      Low likelihood of limb/extremity viability (100% sensitive in validation studies)                      
">
			keywords = <"MESS Score", "Mangled Extremity Severity Score", "Extremity injury", "Limb salvage", "Limb amputation">
			misuse = <"Every effort must be attempted to salvage the limb in upper extremity injuries. 

Newer surgical techniques increase the probability of limb salvage, hence a higher cut off for MESS score, perhaps 8 or 9 must be considered.
">
			copyright = <"@CambioCDS">
		>
	>
	lifecycle_state = <"AuthorDraft">
	other_contributors = <>
	other_details = <
		["licence"] = <"Cambio CDS">
		["references"] = <"1. Johansen K, Daines M, Howey T, Helfet D, Hansen ST. Objective criteria accurately predict amputation following lower extremity trauma. J Trauma. 1990 May;30(5):568–72; discussion 572-573. 

2. Togawa S, Yamami N, Nakayama H, Mano Y, Ikegami K, Ozeki S. The validity of the mangled extremity severity score in the assessment of upper limb injuries. J Bone Joint Surg Br. 2005 Nov;87(11):1516–9. ">
		["MD5-CAM-1.0.1"] = <"ACC195BF4D35FB1441CEC9E207576837">
	>

definition
	OBSERVATION[at0000] matches {	-- Mangled extremity severity score
		data matches {
			HISTORY[at0001] matches {	-- Event Series
				events cardinality matches {1..*; unordered} matches {
					EVENT[at0002] occurrences matches {0..1} matches {	-- Any event
						data matches {
							ITEM_TREE[at0003] matches {	-- Tree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0010] occurrences matches {0..1} matches {	-- Limb ischemia
										value matches {
											1|[local::at0011], 	-- Reduced pulse but normal perfusion
											2|[local::at0012], 	-- Pulseless, paresthesias, slow capillary refill
											3|[local::at0013]  	-- Cool, paralysis, numb/insensate
										}
									}
									ELEMENT[at0031] occurrences matches {0..1} matches {	-- Limb ischemia for > 6 hours
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0032, 	-- Yes
													at0033]	-- No
												}
											}
										}
									}
									ELEMENT[at0014] occurrences matches {0..1} matches {	-- Patient age range
										value matches {
											0|[local::at0015], 	-- <30 yrs
											1|[local::at0016], 	-- 30-50 yrs
											2|[local::at0017]  	-- >50
										}
									}
									ELEMENT[at0018] occurrences matches {0..1} matches {	-- Shock
										value matches {
											0|[local::at0019], 	-- SBP > 90 mmHg Consistently
											1|[local::at0020], 	-- Hypotension Transiently
											2|[local::at0021]  	-- Persistent Hypotension
										}
									}
									ELEMENT[at0022] occurrences matches {0..1} matches {	-- Injury Mechanism
										value matches {
											1|[local::at0023], 	-- Low Energy (stab, gunshot, simple fracture)
											2|[local::at0024], 	-- Medium Energy (dislocation, open/multiple fractures)
											3|[local::at0025], 	-- High Energy (high-speed MVA or rifle shot)
											4|[local::at0026]  	-- Very High Energy (high-speed trauma with gross contamination)
										}
									}
									ELEMENT[at0030] occurrences matches {0..1} matches {	-- Total Score
										value matches {
											DV_COUNT matches {
												magnitude matches {|0..14|}
											}
										}
									}
									ELEMENT[at0034] occurrences matches {0..1} matches {	-- MESS score assessment
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0035, 	-- Low likelihood of limb/extremity viability
													at0036]	-- High likelihood of limb/extremity viability
												}
											}
										}
									}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0004] matches {*}
		}
	}

ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Mangled extremity severity score">
					description = <"A simple rating scale that estimates the viability of an extremity, particularly lower limb after a trauma, helping to determine the need for primary amputation versus salvage.">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Any event">
					description = <"*">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0010"] = <
					text = <"Limb ischemia">
					description = <"Records the points for the ordinal variable 'Limb ischemia'.">
				>
				["at0011"] = <
					text = <"Reduced pulse but normal perfusion">
					description = <"Reduced pulse is observed but perfusion is normal. ">
				>
				["at0012"] = <
					text = <"Pulseless, paresthesias, slow capillary refill">
					description = <"Pulseless, paresthesias, slow capillary refill are observed">
				>
				["at0013"] = <
					text = <"Cool, paralysis, numb/insensate">
					description = <"Cool, paralysis, numb/insensate are observed. ">
				>
				["at0014"] = <
					text = <"Patient age range">
					description = <"Records points given for the ordinal variable 'Patient age range'.">
				>
				["at0015"] = <
					text = <"<30 yrs">
					description = <"Age of the patient is <30 yrs">
				>
				["at0016"] = <
					text = <"30-50 yrs">
					description = <"Age of the patient is between 30 -50 yrs">
				>
				["at0017"] = <
					text = <">50">
					description = <"Age of the patient is >50 yrs">
				>
				["at0018"] = <
					text = <"Shock">
					description = <"Records the points for the ordinal variable Shock.">
				>
				["at0019"] = <
					text = <"SBP > 90 mmHg Consistently">
					description = <"No signs of shock is observed.">
				>
				["at0020"] = <
					text = <"Hypotension Transiently">
					description = <"Transient hypotension is observed">
				>
				["at0021"] = <
					text = <"Persistent Hypotension">
					description = <"Persistent hypotension is observed indicating shock.">
				>
				["at0022"] = <
					text = <"Injury Mechanism">
					description = <"Records the points for the ordinal variable 'Injury Mechanism'.">
				>
				["at0023"] = <
					text = <"Low Energy (stab, gunshot, simple fracture)">
					description = <"Low energy injury">
				>
				["at0024"] = <
					text = <"Medium Energy (dislocation, open/multiple fractures)">
					description = <"Medium energy injury leading to multiple fractures or dislocations.">
				>
				["at0025"] = <
					text = <"High Energy (high-speed MVA or rifle shot)">
					description = <"High energy injury ">
				>
				["at0026"] = <
					text = <"Very High Energy (high-speed trauma with gross contamination)">
					description = <"Very high energy injury ">
				>
				["at0030"] = <
					text = <"Total Score">
					description = <"Total Score is calculated by the addition of points of variables, Limb ischemia, patient age range, shock and injury mechanism. If limb ischemia present for >6 hours, the Points for limb ischemia  is doubled for calculating the MESS Score">
				>
				["at0031"] = <
					text = <"Limb ischemia for > 6 hours">
					description = <"Records Yes/No answer for the variable 'Limb ischemia for > 6 hours'.">
				>
				["at0032"] = <
					text = <"Yes">
					description = <"Limb ischemia present for > 6 hours">
				>
				["at0033"] = <
					text = <"No">
					description = <"Limb ischemia present for <= 6 hours">
				>
				["at0034"] = <
					text = <"MESS score assessment">
					description = <"Predicts the viability of an extremity after trauma based on the MESS Score obtained.">
				>
				["at0035"] = <
					text = <"Low likelihood of limb/extremity viability">
					description = <"MESS Score >=7">
				>
				["at0036"] = <
					text = <"High likelihood of limb/extremity viability">
					description = <"MESS Score <7">
				>
			>
		>
	>
