(GUIDE) <
	gdl_version = <"0.1">
	id = <"Canadian_C_Spine_Rule.v2">
	concept = <"gt0001">
	language = (LANGUAGE) <
		original_language = <[ISO_639-1::en]>
	>
	description = (RESOURCE_DESCRIPTION) <
		details = <
			["en"] = (RESOURCE_DESCRIPTION_ITEM) <
				copyright = <"© Cambio Healthcare Systems">
				keywords = <"canadian c-spine rule", "cervical spine injury">
				misuse = <"Not a substitute for sound clinical judgement.
Do not use if any of the following exclusion criteria are present:
- Non-trauma Patients
- GCS <15
- Unstable Vital Signs
- Age <16 Years
- Acute Paralysis
- Known Vertebral Disease
- Previous C-Spine Surgery">
				purpose = <"Identifying high-risk patients who require radiographic imaging (x-ray, CT etc.) following trauma.">
				use = <"Three criteria are assessed according to the CCR:

1. If the patient's age > 65, if trauma followed a defined dangerous mechanism (see below), or if the patient experiences paresthesias in the arms or legs; then they require c-spine imaging.
*Dangerous mechanism is defined as any of:
Fall from elevation >= 3 feet/5 stairs
Axial load to head e.g. diving
MVC (motor vehicle collision) high speed (>100km/hr), rollover, ejection
Motorized recreational vehicles
Bicycle collision

2. If the patient does not fulfill any criteria in step 1, and meets none of the defined low risk criteria (see list), then they require c-spine imaging.
*Low risk criteria is defined as any of:
Sitting position in the emergency room (ER)
Ambulatory at any time
Delayed (not immediate onset) neck pain
No midline tenderness
Simple rear-end motor vehicle collision (MVC); not simple if: pushed into traffic, hit by bus/large truck, rollover, hit by high-speed vehicle

3. If the patient does not fulfill any criteria in step 1, and meets at least one criteria in step 2, then it is safe to assess whether the patient is able to rotate their neck 45 degrees to the left and right. 
- If they cannot rotate their neck 45 degrees in both directions then they require c-spine imaging.
- If they can do this (even with some pain or discomfort), then they do not require further imaging.">
			>
			["sv"] = (RESOURCE_DESCRIPTION_ITEM) <
				copyright = <"© Cambio Healthcare Systems">
				keywords = <"canadian c-spine rule", "halsryggskada", "halsryggskador", "trauma", "ryggmärg", "ryggmärgskada">
				misuse = <"Använd ej instrumentet om någon av följande exklusionskriterier föreligger:

Ej traumapatient
GCS <15
Instabila vitalparametrar
Ålder <16 år
Akut paralys
Känd vertebral sjukdom
Tidigare genomgång halsryggskirurgi">
				purpose = <"Canadian C-spine Rule (CCR) används för att utan bruk av bilddiagnostik utesluta halsryggskador hos traumapatienter i stabilt, vaket tillstånd.">
				use = <"Exklusionskriterier som måste tas i beaktande återfinns under \"Misuse\".

Inklusionskriterier:

- Om patienten har högriskfaktor (ålder >65, något av alternativen listade under \"Farliga mekanismer\" eller  parestesier i armar eller ben) krävs bilddiagnostik.

Farlig mekanism:

Fall >= 5 trappsteg
Axialt våld mot huvudet
Trafikolycka: >100km/h, voltat, utslängd ur fordonet
Motordrivet fritidsfordon
Cykelolycka

- Om patienten saknar högriskfaktorer men även lågriskfaktorer (listade nedan), krävs bilddiagnostik.

Uppesittande på akuten
Varit uppegående efter inträffad händelse
Fördröjd smärtdebut
Ingen ömhet i nackens medellinje
Enkel påkörning bakifrån: ej inkrockad i mötande trafik, påkörd av buss eller lastbil eller voltat, samt låg hastighet.

- Om halsryggssmärta, avsaknad av högriskfaktorer och minst en lågriskfaktor, är det säkert att testa patientens förmåga till 45 graders rotation åt höger och vänster
- Om rotation är möjlig (även med viss smärta eller obehag), krävs inte vidare bilddiagnostik.
- Om rotation ej är möjlig till 45 grader i båda riktningarna, är bilddiagnostik indicerad.">
			>
		>
		lifecycle_state = <"Author draft">
		original_author = <
			["date"] = <"2017-07-15">
			["email"] = <"models@cambiocds.com">
			["name"] = <"Syeeda S Farruque">
			["organisation"] = <"Cambio Healthcare Systems">
		>
		other_contributors = <"Dennis Forslund, Cambio Healthcare Systems",...>
		other_details = <
			["references"] = <"Stiell IG, Wells GA, Vandemheen KL, Clement CM, Lesiuk H, De Maio VJ, Laupacis A, Schull M, McKnight RD, Verbeek R, Brison R, Cass D, Dreyer J, Eisenhauer MA, Greenberg GH, MacPhail I, Morrison L, Reardon M, Worthington J. The Canadian C-spine rule for radiography in alert and stable trauma patients. JAMA. 2001 Oct 17;286(15):1841-8. PubMed PMID: 11597285">
		>
	>
	definition = (GUIDE_DEFINITION) <
		archetype_bindings = <
			["gt0002"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.canadian_c_spine_rule.v1">
				domain = <"EHR">
				elements = <
					["gt0003"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0004]">
					>
					["gt0004"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0005]">
					>
					["gt0005"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0006]">
					>
				>
				predicates = <"max(/data/events/time)",...>
			>
			["gt0017"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-EVALUATION.canadian_c_spine_assessment.v1">
				domain = <"CDS">
				elements = <
					["gt0018"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/items[at0009]">
					>
				>
			>
			["gt0019"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.basic_demographic.v1">
				domain = <"EHR">
				elements = <
					["gt0020"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0008]">
					>
				>
				predicates = <"max(/data/events/time)",...>
			>
		>
		rules = <
			["gt0014"] = (RULE) <
				when = <"!fired($gt0014)", "($gt0020.value<=($currentDateTime.value-65,a))||(($gt0003==1|local::at0021|Yes|)||((($gt0003==0|local::at0020|No|)&&($gt0004==0|local::at0020|No|))||(($gt0003==0|local::at0020|No|)&&(($gt0004==1|local::at0021|Yes|)&&($gt0005==0|local::at0020|No|)))))">
				then = <"$gt0018=local::at0011|High risk|",...>
				priority = <2>
			>
			["gt0016"] = (RULE) <
				when = <"!fired($gt0016)", "$gt0003==0|local::at0020|No|", "$gt0004==1|local::at0021|Yes|", "$gt0005==1|local::at0021|Yes|">
				then = <"$gt0018=local::at0010|Low risk|",...>
				priority = <1>
			>
		>
	>
	ontology = (GUIDE_ONTOLOGY) <
		term_definitions = <
			["en"] = (TERM_DEFINITION) <
				terms = <
					["gt0001"] = (TERM) <
						text = <"Canadian C-Spine Rule (CCR)">
						description = <"The Canadian C-spine Rule (CCR) helps to safely rule out CSI (Cervical spine injury) in alert and stable trauma patients without a need for radiographic imaging. Three criteria are assessed and if the patient is deemed high risk, radiographic imaging is recommended. In the original patient series, the rule had a sensitivity of 100% (95% confidence interval) and specificity of 42.5% (95% confidence interval) for identifying clinically important C-spine injuries.">
					>
					["gt0003"] = (TERM) <
						text = <"Q1. Age ≥ 65 years, extremity paresthesias or dangerous mechanism (if No go to Q2)">
						description = <"Is the patient's age ≥ 65 years, are they exhibiting extremity paresthesias or subjected to a dangerous mechanism (IF no go to Q2).  Dangerous mechanism:

Fall from elevation >= 3 feet/5 stairs
Axial load to head e.g. diving
MVC (motor vehicle collision) high speed (>100km/hr), rollover, ejection
Motorized recreational vehicles
Bicycle collision">
					>
					["gt0004"] = (TERM) <
						text = <"Q2. Low risk factor present (if Yes – low risk go to Q3)">
						description = <"At least one Low risk factor including: sitting position in the ED
Ambulatory at any time
Delayed (not immediate onset) neck pain
No midline tenderness. 
Simple rear-end motor vehicle collision (MVC) not simple if: pushed into traffic, hit by bus/large truck, rollover, hit by high-speed vehicle.">
					>
					["gt0005"] = (TERM) <
						text = <"Q3. Able to actively rotate neck 45° left and right (if No – high risk)">
						description = <"Assess whether the patient is able to rotate their neck 45 degrees to the left and right (even with some pain or discomfort).">
					>
					["gt0014"] = (TERM) <
						text = <"Calculate High risk level">
					>
					["gt0016"] = (TERM) <
						text = <"Calculate Low risk level">
					>
					["gt0018"] = (TERM) <
						text = <"Risk assessment">
						description = <"*">
					>
					["gt0020"] = (TERM) <
						text = <"Birthdate">
						description = <"The patient's date of birth.">
					>
					["gt0021"] = (TERM) <
						text = <"Set age ≥65 years">
					>
					["gt0023"] = (TERM) <
						text = <"Q1. Age ≥ 65 years, extremity paresthesias or dangerous mechanism (if No go to Q2)">
						description = <"Is the patient ≥ 65 years, are they exhibiting extremity paresthesias or subjected to a dangerous mechanism (IF no go to Q2).  [Dangerous mechanism: 

Fall from elevation >= 3 feet/5 stairs, 
Axial load to head e.g. diving, 
MVC (motor vehicle collision) high speed (>100km/hr), rollover, ejection, 
Motorized recreational vehicles
Bicycle collision]">
					>
				>
			>
			["sv"] = (TERM_DEFINITION) <
				terms = <
					["gt0001"] = (TERM) <
						text = <"Canadian C-Spine Rule">
						description = <"Canadian C-spine Rule (CCR) används för att utan bruk av bilddiagnostik utesluta halsryggskador hos traumapatienter i stabilt, vaket tillstånd.">
					>
					["gt0003"] = (TERM) <
						text = <"Q1. Ålder ≥ 65 år, extremitetsparestesi eller farlig mekanism? (om nej - gå vidare till Q2)">
						description = <"*(en) Is the patient's age ≥ 65 years, are they exhibiting extremity paresthesias or subjected to a dangerous mechanism (IF no go to Q2).  Dangerous mechanism:

Fall from elevation >= 3 feet/5 stairs
Axial load to head e.g. diving
MVC (motor vehicle collision) high speed (>100km/hr), rollover, ejection
Motorized recreational vehicles
Bicycle collision">
					>
					["gt0004"] = (TERM) <
						text = <"Q2. Föreliggande lågriskfaktor? (om ja - gå vidare till Q3)">
						description = <"*(en) At least one Low risk factor including: sitting position in the ED
Ambulatory at any time
Delayed (not immediate onset) neck pain
No midline tenderness. 
Simple rear-end motor vehicle collision (MVC) not simple if: pushed into traffic, hit by bus/large truck, rollover, hit by high-speed vehicle.">
					>
					["gt0005"] = (TERM) <
						text = <"Q3. Aktiv rotation i 45° höger och vänster möjlig?">
						description = <"*(en) Assess whether the patient is able to rotate their neck 45 degrees to the left and right (even with some pain or discomfort).">
					>
					["gt0014"] = (TERM) <
						text = <"CDS hög risk">
					>
					["gt0016"] = (TERM) <
						text = <"CDS låg risk">
					>
					["gt0018"] = (TERM) <
					>
					["gt0020"] = (TERM) <
					>
					["gt0021"] = (TERM) <
					>
					["gt0023"] = (TERM) <
					>
				>
			>
		>
	>
>
